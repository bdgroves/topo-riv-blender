# Render Parameters File
module_name = "blender_parameters.blender_params_global"

# Import parameters file
import os, sys, importlib
render_params = module_name.replace(".", os.sep) + ".py"
parameters = importlib.import_module(module_name)

# Define the final output
rule all:
    input:
        "phase_2_visualize/out/render_mtfuji.png",
        "fonts/" + parameters.label_font.replace(" ", "_") + ".ttf"

# Download Google Font
rule download_font:
    params:
        label_font = parameters.label_font
    output:
        "fonts/" + parameters.label_font.replace(" ", "_") + ".ttf"
    script:
        "phase_0_fetch/src/download_font.py"

# Creates a multipoint shapefile for labeling
rule create_shapefile:
    params:
        coords = [[35.3606, 138.7274],[35.3438, 138.7522]],
        labels = ["Mt. Fuji", "Hoeizan"],
        pin_colors = [[0.75,0.0,0.0,1.0],[0.0,0.0,0.75,1.0]],
        label_colors = [[0.0,0.0,0.0,1.0],[0.0,0.0,0.0,1.0]],
        crs = "EPSG:4326"
    output:
        shpfile = "phase_0_fetch/out/{name}/labels.shp"
    script:
        "phase_0_fetch/src/make_shpfile.py"

# Make the bounding box
rule make_bounding_box:
    params:
        UL_corner = [35.6, 138.5],
        LR_corner = [35.1, 139.0],
    output:
        extent_shpfile = "phase_0_fetch/out/{name}/extent.shp"
    script:
        "phase_0_fetch/src/make_bounding_box.py"

# Downloads a digital elevation model within extent 
rule download_dem:
    params:
        dem_product = parameters.dem_product,
        render_pixels = parameters.res_x * parameters.res_y,
        buffer = parameters.buffer
    input:
        extent_shpfile = "phase_0_fetch/out/{name}/extent.shp"
    output:
        demfile = "phase_0_fetch/out/{name}/dem.tif"
    script:
        "phase_0_fetch/src/download_dem.py"

# Downloads aerial imagery within extent
rule download_aerial:
    params:
        query = {
                "eo:cloud_cover": {"lte": 25},
                "platform":       {"in": ["landsat-8"]}
                },
        start_query = "2021-11-01",
        end_query = "2022-02-01"
    input:
        extent_shpfile = "phase_0_fetch/out/{name}/extent.shp"
    output:
        out_bands = ["phase_0_fetch/out/{name}/" + color + ".tif" for color in ["red", "green", "blue"]]
    script:
        "phase_0_fetch/src/get_imagery.py"

# Creates a grayscale height map and texture map for Blender to render.
# The height map is used to determine how high the landscape should be 
# in the render, and the texture map determines the color of the landscape.
rule create_heightmap_texturemap:
    params:
        map_crs = "EPSG:3857",
        buffer = parameters.buffer,
        background_color = parameters.background_color,
        wall_color = parameters.wall_color
    input:
        extent_shpfile = "phase_0_fetch/out/{name}/extent.shp",
        demfile =  "phase_0_fetch/out/{name}/dem.tif",
        aerialfiles =  ["phase_0_fetch/out/{name}/" + color + ".tif" for color in ["red", "green", "blue"]],
        labels_shpfile = "phase_0_fetch/out/{name}/labels.shp"
    output:
        dimensions_file = "phase_1_process/out/{name}/dimensions.npy",
        heightmap_file = "phase_1_process/out/{name}/heightmap.png",
        texturemap_file = "phase_1_process/out/{name}/texturemap.png",
        apronmap_file = "phase_1_process/out/{name}/apronmap.png",
        labels_file = "phase_1_process/out/{name}/labels.csv"
    script:
        "phase_1_process/src/process.py"

# Using the height map and texture map, Blender sets up the scene 
# (topography, lighting, and camera) and renders a photorealistic image.
rule render:
    input:
        dimensions_file = "phase_1_process/out/{name}/dimensions.npy",
        heightmap_file = "phase_1_process/out/{name}/heightmap.png",
        texturemap_file = "phase_1_process/out/{name}/texturemap.png",
        apronmap_file = "phase_1_process/out/{name}/apronmap.png",
        labels_file = "phase_1_process/out/{name}/labels.csv",
        blender_params = render_params,
        label_fontfile = "fonts/" + parameters.label_font.replace(" ", "_") + ".ttf"
    output:
        output_file = "phase_2_visualize/out/render_{name}.png"
    shell:
        "Blender -b -P phase_2_visualize/src/render.py -- "
        "{input.blender_params} "
        "{input.dimensions_file} {input.heightmap_file} {input.texturemap_file} {input.apronmap_file} {input.labels_file} "
        "{output.output_file} "